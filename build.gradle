buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

dependencies {
  compile 'commons-codec:commons-codec:1.10'
  compile 'commons-io:commons-io:2.4'
  compile 'com.zaxxer:HikariCP'
  compile 'org.apache.httpcomponents:httpclient:4.3.6'
  compile 'org.postgresql:postgresql:9.4-1201-jdbc41'
  compile 'org.springframework.boot:spring-boot-starter-actuator'
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.springframework.boot:spring-boot-starter-jdbc'
  compile 'org.springframework.boot:spring-boot-starter-security'
  compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.flywaydb:flyway-core'

  integrationTestCompile 'info.cukes:cucumber-java8:1.2.4'
  integrationTestCompile 'info.cukes:cucumber-junit:1.2.4'
  integrationTestCompile 'info.cukes:cucumber-spring:1.2.4'
  integrationTestCompile 'org.seleniumhq.selenium:selenium-java:2.41.0'
  integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
   jvmArgs = ["-Dspring.profiles.active=test"]
}

eclipse {
  classpath {
    plusConfigurations += [configurations.integrationTestCompile]
  }
}

task stage (dependsOn: build)

task integrationTest (type:JavaExec, dependsOn:integrationTestClasses) {
  if (project.hasProperty('features')) {
    args += ['--name', "${features}"]
  }
  args += ['--strict', '-p', "html:${buildDir}/reports/tests/integration", '--glue', 'com.appmoney.integrationtest.glue', 'src/integrationTest/features/Category.feature']

  classpath = project.sourceSets.integrationTest.runtimeClasspath
  description = "Run integration tests. Add your features to src/integrationTest/features. Your step definitions should go inside package com.bearprogrammer.blog.sample.integrationTest.glue and your java classes to src/integrationTest/java."
  group = 'Verification'
  main = 'cucumber.api.cli.Main'
  mustRunAfter test // We don't want to run integration tests if functional test haven't past

  jvmArgs = ['-Dspring.profiles.active=integrationTest']
}
